<?xml version="1.0"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" creationComplete="onComplete(event)">
    <fx:Metadata>
        [SWF(height=100,width=100,backgroundColor="0x000000")]
    </fx:Metadata>
    <fx:Script><![CDATA[
        import avmplus.factoryXml;

        import mx.core.FlexGlobals;
        import mx.events.FlexEvent;
        import mx.utils.ObjectUtil;

        import org.osmf.elements.AudioElement;
        import org.osmf.events.MediaPlayerStateChangeEvent;
        import org.osmf.media.MediaPlayer;
        import org.osmf.media.MediaPlayerState;
        import org.osmf.media.URLResource;

        import ru.teachbase.utils.Logger;

        private var _player:MediaPlayer;

        [Embed(source="../assets/pause.png")]
        private var PauseIcon:Class;

        [Embed(source="../assets/play.png")]
        private var PlayIcon:Class;

        private var defaults:Object = {
            playIcon: new PlayIcon(),
            pauseIcon: new PauseIcon(),
            width: 100,
            height: 100,
            name: 'audio'
        }

        private function onComplete(event:FlexEvent):void {

            _player = new MediaPlayer();

            _player.addEventListener(MediaPlayerStateChangeEvent.MEDIA_PLAYER_STATE_CHANGE, onStateChanged);

            var params:Object = FlexGlobals.topLevelApplication.parameters;

            for (var key:String in defaults) {
                if (!params[key]) {
                    params[key] = defaults[key];
                }
            }

            controls.width = params.width;
            controls.height = params.height;

            pause_btn.source = params.pauseIcon;
            play_btn.source = params.playIcon;

            Logger.log(ObjectUtil.toString(params.name));


            if (ExternalInterface.available) {
                ExternalInterface.addCallback("loadAudio", load);
                ExternalInterface.addCallback("call", call);
                Logger.log("inited");

                ExternalInterface.call(params.name+'.initialize');
            }
        }


        public function load(source:String,autoPlay:Boolean = false, loop:Boolean = false):void {

            if (_player.media) _player.stop();

            var _resource:URLResource = new URLResource(source);
            var _audio:AudioElement = new AudioElement(_resource);

            _player.volume = .9;
            _player.autoPlay = autoPlay;
            _player.loop = loop;
            _player.media = _audio;

        }

        public function call(method:String) {

            if (_player.hasOwnProperty(method)) {
                _player[method]();
            }

        }


        private function control_clickHandler(event:MouseEvent):void {

            if (_player.state === MediaPlayerState.PLAYING) _player.pause();
            else _player.play();

        }


        private function onStateChanged(event:MediaPlayerStateChangeEvent):void {

            Logger.log(event.state);
            play_btn.visible = !(pause_btn.visible = (event.state === MediaPlayerState.PLAYING));

        }


        ]]></fx:Script>
   <s:Group id="controls" click="control_clickHandler(event)">
        <s:BitmapImage id="pause_btn" width="100%" height="100%" visible="false"/>
        <s:BitmapImage id="play_btn" width="100%" height="100%"/>
   </s:Group>
</s:Application>
